# Compute installation prefix relative to this file.
get_filename_component(_dir "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_prefix "${_dir}/../.." ABSOLUTE)

# Import the targets.
include("${_prefix}/lib/@PROJECT_NAME@-@PROJECT_VERSION@/@PROJECT_NAME@-targets.cmake")

# Report other information.
set(@PROJECT_NAME@_INCLUDE_DIRS "${_prefix}/include/@PROJECT_NAME@-@PROJECT_VERSION@")

find_program (PREPARE_SHADER_COMMAND prepare_shader REQUIRED)
find_program (SPIRV_COMPILER glslc REQUIRED)

function (add_shader TARGET_NAME SOURCE)
    add_custom_command(
        OUTPUT
            ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}/${TARGET_NAME}.spv
        DEPENDS
            ${SOURCE}
        COMMAND
            ${CMAKE_COMMAND} -E make_directory ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}
        COMMAND
            ${SPIRV_COMPILER} --target-env=vulkan1.0 -o ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}/${TARGET_NAME}.spv ${SOURCE}
    )

    add_custom_command(
        OUTPUT
            ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}/${TARGET_NAME}.cpp
            ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}/${TARGET_NAME}.hpp
        DEPENDS
            ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}/${TARGET_NAME}.spv
        COMMAND ${PREPARE_SHADER_COMMAND} ${TARGET_NAME} ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}/${TARGET_NAME}.spv
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}
    )
    
    add_library(${TARGET_NAME} ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}/${TARGET_NAME}.cpp)
    target_include_directories (${TARGET_NAME} 
        PUBLIC
            ${CMAKE_CURRENT_BINARY_DIR}/${TARGET_NAME}
    )
    set_target_properties(${TARGET_NAME} PROPERTIES CXX_STANDARD 17)
endfunction()
